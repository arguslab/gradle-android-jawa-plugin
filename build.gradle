import org.argus.jawa.android.gradle.AndroidJawaPluginIntegrationTestTask

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.5"
    }
}

apply plugin: "java"
apply plugin: "groovy"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "idea"
apply plugin: "com.gradle.plugin-publish"

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "commons-io:commons-io:2.4"
    compile "gradle.plugin.com.github.arguslab:gradle-jawa-plugin:1.0.5"
    compile group: 'com.github.arguslab', name: 'jawa-compiler_2.11', version: '1.0.5'
    testCompile "com.android.tools.build:gradle:2.0.0"
}

group = "com.github.arguslab"
version = "1.0.3-SNAPSHOT"
description = "Gradle Android Jawa Plugin adds jawa language support to official gradle android plugin."

sourceCompatibility = JavaVersion.VERSION_1_6
targetCompatibility = JavaVersion.VERSION_1_6

final boolean isSnapshot = project.version.endsWith('-SNAPSHOT')

def uploadToSonatypeRepository = hasProperty("sonatypeUserName")

uploadArchives {
    repositories {
        mavenDeployer {
            snapshotRepository(url: uri("gh-pages/repository/snapshot"))

            if (uploadToSonatypeRepository) {
                def repositoryUrl = isSnapshot ? "https://oss.sonatype.org/content/repositories/snapshots"
                                                                  : "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: repositoryUrl) {
                    authentication(userName: sonatypeUserName, password: sonatypePassword)
                }
                pom.project {
                    name "Gradle Android Jawa Plugin"
                    packaging "jar"
                    description project.description
                    url "https://github.com/arguslab/gradle-android-jawa-plugin"

                    scm {
                        url "scm:git@github.com:arguslab/gradle-android-jawa-plugin.git"
                        connection "scm:git@github.com:arguslab/gradle-android-jawa-plugin.git"
                        developerConnection "scm:git@github.com:arguslab/gradle-android-jawa-plugin.git"
                    }

                    licenses {
                        license {
                            name "Eclipse-1.0"
                            url "http://www.opensource.org/licenses/eclipse-1.0.php"
                        }
                    }

                    developers {
                        developer {
                            id "fgwei"
                            name "Fengguo (Hugo) Wei"
                            email 'fgwei521@gmail.com'
                            organization 'arguslab'
                        }
                    }
                }
            }
        }
    }
}


pluginBundle {
    website = 'https://github.com/arguslab/gradle-android-jawa-plugin'
    vcsUrl = 'https://github.com/arguslab/gradle-android-jawa-plugin.git'
    description = project.description
    tags = ['jawa', 'android']

    plugins {
        androidJawaPlugin {
            id = 'org.argus.android-jawa'
            displayName = 'Android Jawa Gradle Plugin'
        }
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    description 'generate sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    description 'generate javadoc'
    from javadoc
}

artifacts {
    archives sourceJar
    archives javadocJar
}

task integTest(type: AndroidJawaPluginIntegrationTestTask, dependsOn: "uploadArchives")

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

signing {
    required { !isSnapshot && gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

// the gradle plugin portal isn't compatible with SNAPSHOT versions
// https://discuss.gradle.org/t/uploading-snapshot-versions-to-the-plugin-portal/11347
if (isSnapshot) {
    signArchives.enabled = false
    publishPlugins.enabled = false
} else {
    signArchives.enabled = true
    publishPlugins.enabled = true
}